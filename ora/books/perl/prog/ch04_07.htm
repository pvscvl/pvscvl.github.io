<html><head>
<title>[Chapter 4] 4.7 Data Structure Code Examples</TITLE>
<meta name="DC.title" content="Programming Perl"><meta name="DC.creator" content="Larry Wall, Tom Christiansen &amp; Randal L. Schwartz"><meta name="DC.publisher" content="O'Reilly &amp; Associates, Inc."><meta name="DC.date" content="1999-07-02T01:57:48Z"><meta name="DC.type" content="Text.Monograph"><meta name="DC.format" content="text/html" scheme="MIME"><meta name="DC.source" content="1-56592-149-6" scheme="ISBN"><meta name="DC.language" content="en-US"><meta name="generator" content="Jade 1.1/O'Reilly DocBook 3.0 to HTML 4.0"><link rev="made" href="mailto:online-books@oreilly.com" title="Online Books Comments"><link rel="up" href="ch04_01.htm" title="4. References and Nested Data Structures"><link rel="prev" href="ch04_06.htm" title="4.6 A Brief Tutorial:  Manipulating Lists of Lists"><link rel="next" href="ch05_01.htm" title="5. Packages, Modules, and Object Classes"></HEAD
><body bgcolor="#FFFFFF" text="#000000"><div class="htmlnav"><h1><img src="gifs/smbanner.gif" alt="Programming Perl" usemap="#srchmap" border="0"></H1
><map name=index.html"srchmap"><area shape="RECT" coords="0,0,466,65" href="index.htm" alt="Programming Perl"><area shape="RECT" coords="467,0,514,18" href="../search/psrch.htm" alt="Search this book"></MAP
><table width="515" border="0" cellspacing="0" cellpadding="0"><tr><td align="LEFT" valign="TOP" width="172"><a class="SECT1" href="ch04_06.htm" title="4.6 A Brief Tutorial:  Manipulating Lists of Lists"><img src="../gifs/txtpreva.gif" alt="Previous: 4.6 A Brief Tutorial:  Manipulating Lists of Lists" border="0"></A
></TD
><td align="CENTER" valign="TOP" width="171"><b><font face="ARIEL,HELVETICA,HELV,SANSERIF" size="-1"><a class="chapter" rel="up" href="ch04_01.htm" title="4. References and Nested Data Structures">Chapter 4<br>References and Nested Data Structures</A
></FONT
></B
></TD
><td align="RIGHT" valign="TOP" width="172"><a class="CHAPTER" href="ch05_01.htm" title="5. Packages, Modules, and Object Classes"><img src="../gifs/txtnexta.gif" alt="Next: 5. Packages, Modules, and Object Classes" border="0"></A
></TD
></TR
></TABLE
>&nbsp;<hr align="LEFT" width="515" title="footer"></DIV
><div class="SECT1"><h2 class="sect1"><a class="title" name="PERL2-CH-4-SECT-7">4.7 Data Structure Code Examples</A
></H2
><p class="para"><a class="indexterm" name="CH04.NEST2"></A
><a class="indexterm" name="CH04.NEST1"></A
>Once you've mastered creating and using multi-dimensional arrays (lists of lists),
you'll want to be able to make more complex data structures.  If you're
looking for C structures or Pascal records, you won't find any special 
reserved words in Perl to set these up for you.  What you get instead
is a more flexible system.[<a class="footnote" href="#AUTOID-14995">8</A
>]
Perl has just two ways of organizing data:
either as ordered lists stored in arrays and accessed by position,
or as unordered key/value pairs stored in hashes and accessed by name.</P
><blockquote class="footnote"><div class="footnote"><p class="para"><a class="footnote" name="AUTOID-14995">[8]</A
> If your idea of a record structure
is less flexible than this, or if you'd like to provide your users with 
something more opaque and rigid, then you can use the object-oriented
features detailed in <a class="xref" href="ch05_01.htm" title="Packages, Modules, and Object Classes">Chapter 5</A
>.</P
></DIV
></BLOCKQUOTE
><p class="para">The best way to represent a record in Perl is using a hash reference, but how you
choose to organize such records will vary.  You may wish to keep an ordered list of
these records around that you can look up by number, in which case you'd
use an array to store the records (hash references).  But you might wish to look up
the records by name, in which case you'd store them in another hash.
You could even do both at once: the array and the hash could each hold
references to the same records, which are after all just anonymous hash
thingies, and each one
can have as many references to it as you want, within reason.[<a class="footnote" href="#AUTOID-14999">9</A
>]</P
><blockquote class="footnote"><div class="footnote"><p class="para"><a class="footnote" name="AUTOID-14999">[9]</A
> Where reason is defined as 2**32 references, minus one.  That's probably
sufficient for most folks.</P
></DIV
></BLOCKQUOTE
><p class="para">In the following sections you will find code examples detailing how to compose,
generate, access, and print out each of five data structures.  The
first four examples are straightforward homogeneous combinations of arrays and hashes, while
the last one demonstrates how to use a less regular data structure.  These examples,
presented with little comment, assume that you have already familiarized
yourself with the earlier explanations set forth in this chapter.</P
><div class="sect2"><h3 class="sect2"><a class="title" name="PERL2-CH-4-SECT-7.1">4.7.1 Arrays of Arrays</A
></H3
><p class="para"><a class="indexterm" name="AUTOID-15005"></A
><a class="indexterm" name="AUTOID-15008"></A
>Use an array of arrays when you want a basic two-dimensional matrix. 
One application might include making a list of all the hosts on your network, 
but each of these hosts would have several possible aliases.  Another 
might be a list of daily menus, each of which would itself be a list of foods 
served in it.  For our example, we'll keep several lists of 
famous television characters, all stored together in one large list of lists.</P
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.1.1">4.7.1.1 Composition of an array of arrays</A
></H4
><pre class="programlisting">@LoL = (
    [ &quot;fred&quot;, &quot;barney&quot; ],
    [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
    [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
);</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.1.2">4.7.1.2 Generation of an array of arrays</A
></H4
><pre class="programlisting"># reading from a file
while ( &lt;&gt; ) {
    push @LoL, [ split ];
}

# calling a function
for $i ( 1 .. 10 ) {
    $LoL[$i] = [ somefunc($i) ];
}

# using temp vars
for $i ( 1 .. 10 ) {
    @tmp = somefunc($i);
    $LoL[$i] = [ @tmp ];
}

# add to an existing row
push @{ $LoL[0] }, &quot;wilma&quot;, &quot;betty&quot;;</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.1.3">4.7.1.3 Access and printing of an array of arrays</A
></H4
><a class="indexterm" name="AUTOID-15018"></A
><pre class="programlisting"># one element
$LoL[0][0] = &quot;Fred&quot;;

# another element
$LoL[1][1] =~ s/(\w)/\u$1/;

# print the whole thing with refs
for $array_ref ( @LoL ) {
    print &quot;\t [ @$array_ref ],\n&quot;;
}

# print the whole thing with indices
for $i ( 0 .. $#LoL ) {
    print &quot;\t [ @{$LoL[$i]} ],\n&quot;;
}

# print the whole thing one at a time
for $i ( 0 .. $#LoL ) {
    for $j ( 0 .. $#{$LoL[$i]} ) {
        print &quot;element $i $j is $LoL[$i][$j]\n&quot;;
    }
}</PRE
></DIV
></DIV
><div class="sect2"><h3 class="sect2"><a class="title" name="PERL2-CH-4-SECT-7.2">4.7.2 Hashes of Arrays</A
></H3
><p class="para"><a class="indexterm" name="AUTOID-15025"></A
>Use a hash of arrays when you want to look up each array by 
a particular string rather than merely by an index number.  In our 
example of television characters, rather than merely looking up the
list of names by the zeroth show, the first show, and so on, 
we'll set it up so we can look up the cast list according to 
the name of the show.</P
><p class="para">Because our outer data structure is a hash, we've lost all ordering of its
contents.  That means when you print it out, you can't predict the order
things will come out.  You can call the <a class="xref" href="ch03_153.htm#PERL2-CMD-SORT" title="sort"><b class="xref.cmd">sort</B
></A
> function and
print its result if you'd like a particular output order.</P
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.2.1">4.7.2.1 Composition of a hash of arrays</A
></H4
><pre class="programlisting"># we customarily omit quotes when keys are identifiers
%HoL = (
    flintstones    =&gt; [ &quot;fred&quot;, &quot;barney&quot; ],
    jetsons        =&gt; [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
    simpsons       =&gt; [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
);</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.2.2">4.7.2.2 Generation of a hash of arrays</A
></H4
><pre class="programlisting"># reading from file with the following format:
# flintstones: fred barney wilma dino
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    $HoL{$1} = [ split ];
}

# reading from file; more temporary variables
# flintstones: fred barney wilma dino
while ( $line = &lt;&gt; ) {
    ($who, $rest) = split /:\s*/, $line, 2;
    @fields = split ' ', $rest;
    $HoL{$who} = [ @fields ];
}

# calling a function that returns an array
for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
    $HoL{$group} = [ get_family($group) ];
}

# likewise, but using temporary variables
for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
    @members = get_family($group);
    $HoL{$group} = [ @members ];
}

# append new members to an existing family
push @{ $HoL{flintstones} }, &quot;wilma&quot;, &quot;betty&quot;;</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.2.3">4.7.2.3 Access and printing of a hash of arrays</A
></H4
><a class="indexterm" name="AUTOID-15038"></A
><pre class="programlisting"># one element
$HoL{flintstones}[0] = &quot;Fred&quot;;

# another element
$HoL{simpsons}[1] =~ s/(\w)/\u$1/;

# print the whole thing
foreach $family ( keys %HoL ) {
    print &quot;$family: @{ $HoL{$family} }\n&quot;;
}
# print the whole thing with indices
foreach $family ( keys %HoL ) {
    print &quot;$family: &quot;;
    foreach $i ( 0 .. $#{ $HoL{$family} } ) {
        print &quot; $i = $HoL{$family}[$i]&quot;;
    }
}
print &quot;\n&quot;;

# print the whole thing sorted by number of members
foreach $family ( sort { @{$HoL{$b}} &lt;=&gt; @{$HoL{$a}} } keys %HoL ) {
    print &quot;$family: @{ $HoL{$family} }\n&quot;
}
# print the whole thing sorted by number of members and name
foreach $family ( sort { @{$HoL{$b}} &lt;=&gt; @{$HoL{$a}} } keys %HoL ) {
    print &quot;$family: &quot;, join(&quot;, &quot;, sort @{ $HoL{$family} }), &quot;\n&quot;;
}</PRE
></DIV
></DIV
><div class="sect2"><h3 class="sect2"><a class="title" name="PERL2-CH-4-SECT-7.3">4.7.3 Arrays of Hashes</A
></H3
><p class="para"><a class="indexterm" name="AUTOID-15045"></A
>An array of hashes is called for when you have a bunch of records
that you'd like to access sequentially, but each record itself contains
key/value pairs.  These arrays tend to be used less frequently than the
other homogeneous data structures.</P
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.3.1">4.7.3.1 Composition of an array of hashes</A
></H4
><pre class="programlisting">@LoH = (
    {
       lead     =&gt; &quot;fred&quot;,
       friend   =&gt; &quot;barney&quot;,
    },
    {
       lead    =&gt; &quot;george&quot;,
       wife    =&gt; &quot;jane&quot;,
       son     =&gt; &quot;elroy&quot;,
    },
    {
       lead    =&gt; &quot;homer&quot;,
       wife    =&gt; &quot;marge&quot;,
       son     =&gt; &quot;bart&quot;,
    },
  );</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.3.2">4.7.3.2 Generation of an array of hashes</A
></H4
><pre class="programlisting"># reading from file
# format: lead=fred friend=barney
while ( &lt;&gt; ) {
    $rec = {};
    for $field ( split ) {
        ($key, $value) = split /=/, $field;
        $rec-&gt;{$key} = $value;
    }
    push @LoH, $rec;
}

# reading from file
# format: lead=fred friend=barney
# no temp
while ( &lt;&gt; ) {
    push @LoH, { split /[\s=]+/ };
}

# calling a function that returns a key,value array, like
# &quot;lead&quot;,&quot;fred&quot;,&quot;daughter&quot;,&quot;pebbles&quot;
while ( %fields = getnextpairset() ) {
    push @LoH, { %fields };
}

# likewise, but using no temp vars
while (&lt;&gt;) {
    push @LoH, { parsepairs($_) };
}

# add key/value to an element
$LoH[0]{pet} = &quot;dino&quot;;
$LoH[2]{pet} = &quot;santa's little helper&quot;;</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.3.3">4.7.3.3 Access and printing of an array of hashes</A
></H4
><a class="indexterm" name="AUTOID-15056"></A
><pre class="programlisting"># one element
$LoH[0]{lead} = &quot;fred&quot;;

# another element
$LoH[1]{lead} =~ s/(\w)/\u$1/;

# print the whole thing with refs
for $href ( @LoH ) {
    print &quot;{ &quot;;
    for $role ( keys %$href ) {
         print &quot;$role=$href-&gt;{$role} &quot;;
    }
    print &quot;}\n&quot;;
}

# print the whole thing with indices
for $i ( 0 .. $#LoH ) {
    print &quot;$i is { &quot;;
    for $role ( keys %{ $LoH[$i] } ) {
         print &quot;$role=$LoH[$i]{$role} &quot;;
    }
    print &quot;}\n&quot;;
}

# print the whole thing one at a time
for $i ( 0 .. $#LoH ) {
    for $role ( keys %{ $LoH[$i] } ) {
         print &quot;element $i $role is $LoH[$i]{$role}\n&quot;;
    }
}</PRE
></DIV
></DIV
><div class="sect2"><h3 class="sect2"><a class="title" name="PERL2-CH-4-SECT-7.4">4.7.4 Hashes of Hashes</A
></H3
><p class="para"><a class="indexterm" name="AUTOID-15063"></A
><a class="indexterm" name="AUTOID-15066"></A
>A multi-dimensional hash is the most flexible of Perl's 
homogeneous structures.  It's like building up a record 
that itself contains other records.  At each level you index
into the hash with a string (quoted if it contains spaces).  Remember,
however, that the key/value pairs in the hash won't come out in any
particular order.  You must do your own sorting if the order matters.</P
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.4.1">4.7.4.1 Composition of a hash of hashes</A
></H4
><pre class="programlisting">%HoH = (
    flintstones =&gt; {
        lead      =&gt; &quot;fred&quot;,
        pal       =&gt; &quot;barney&quot;,
    },
    jetsons =&gt; {
        lead      =&gt; &quot;george&quot;,
        wife      =&gt; &quot;jane&quot;,
        &quot;his boy&quot; =&gt; &quot;elroy&quot;,  # key quotes needed
    },
    simpsons =&gt; {
        lead      =&gt; &quot;homer&quot;,
        wife      =&gt; &quot;marge&quot;,
        kid       =&gt; &quot;bart&quot;,
    },
);</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.4.2">4.7.4.2 Generation of a hash of hashes</A
></H4
><pre class="programlisting"># reading from file
# flintstones: lead=fred pal=barney wife=wilma pet=dino
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    $who = $1;
    for $field ( split ) {
        ($key, $value) = split /=/, $field;
        $HoH{$who}{$key} = $value;
    }
}

# reading from file; more temporary variables
while ( &lt;&gt; ) {
    next unless s/^(.*?):\s*//;
    $who = $1;
    $rec = {};
    $HoH{$who} = $rec;
    for $field ( split ) {
        ($key, $value) = split /=/, $field;
        $rec-&gt;{$key} = $value;
    }
}

# calling a function that returns a key,value for the inner hash
for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
    $HoH{$group} = { get_family($group) };
}

# likewise, but using temporary variables
for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
    %members = get_family($group);
    $HoH{$group} = { %members };
}

# calling a function that returns the outer hash, including
# references to the created inner hashes
sub hash_families {
    my @ret;
    foreach $group ( @_ ) {
        push @ret, $group, { get_family($group) };
    }
    @ret;
}
%HoH = hash_families( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; );

# append new members to an existing family
%new_folks = (
    wife =&gt; &quot;wilma&quot;,
    pet  =&gt; &quot;dino&quot;;
);
for $what (keys %new_folks) {
    $HoH{flintstones}{$what} = $new_folks{$what};
}</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.4.3">4.7.4.3 Access and printing of a hash of hashes</A
></H4
><a class="indexterm" name="AUTOID-15076"></A
><pre class="programlisting"># one element
$HoH{flintstones}{wife} = &quot;wilma&quot;;

# another element
$HoH{jetsons}{'his boy'} =~ s/(\w)/\u$1/;

# print the whole thing
foreach $family ( keys %HoH ) {
    print &quot;$family: &quot;;
    foreach $role ( keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
    }
    print &quot;\n&quot;;
}

# print the whole thing, using temporaries
while ( ($family,$roles) = each %HoH ) {
    print &quot;$family: &quot;;
    while ( ($role,$person) = each %$roles ) {  # using each precludes sorting
        print &quot;$role=$person &quot;;
    }
    print &quot;\n&quot;;
}

# print the whole thing somewhat sorted
foreach $family ( sort keys %HoH ) {
    print &quot;$family: &quot;;
    foreach $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
    }
    print &quot;\n&quot;;
}

# print the whole thing sorted by number of members
foreach $family ( sort { keys %{$HoH{$a}} &lt;=&gt; keys %{$HoH{$b}} } keys %HoH ) {
    print &quot;$family: &quot;;
    foreach $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
    }
    print &quot;\n&quot;;
}

# establish a sort order (rank) for each role
$i = 0;
for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }

# now print the whole thing sorted by number of members
foreach $family ( sort { keys %{$HoH{$a}} &lt;=&gt; keys %{$HoH{$b}} } keys %HoH ) {
    print &quot;$family: &quot;;

    # and print these according to rank order
    foreach $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} } keys %{ $HoH{$family} } ) {
        print &quot;$role=$HoH{$family}{$role} &quot;;
    }
    print &quot;\n&quot;;
}</PRE
></DIV
></DIV
><div class="sect2"><h3 class="sect2"><a class="title" name="PERL2-CH-4-SECT-7.5">4.7.5 More Elaborate Records</A
></H3
><p class="para"><a class="indexterm" name="CH04.REC"></A
>Those were simple, two-level, homogeneous data structures: each
element contains the same kind of thingy as all the other elements do.
It certainly doesn't have to be that way.  Any element can hold any
kind of scalar, which means that it could be a string, a number, or a
reference to anything at all, including more exotic things than just
array or hash references, such as references to named or anonymous
functions or opaque objects.  The only thing you
can't do is to put more than one kind of thingy
into a given scalar simultaneously.  If you find yourself trying to do
that, it's a signal that you need to establish an array or hash at the
next lower level to handle the different types of thingy you're trying
to overlay.</P
><p class="para">Below you will find code examples designed to illustrate
all the possible kinds of things you might want to keep in 
a record.  For our base structure, we'll use a hash reference.
The keys are uppercase strings, a convention sometimes employed 
when the hash is being used as a specific record type rather than 
as a more generic associative array.</P
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.5.1">4.7.5.1 Composition of more elaborate records</A
></H4
><p class="para">This shows how to create and use a record whose fields are of
many sorts:</P
><pre class="programlisting">$rec = {
    TEXT      =&gt; $string,
    SEQUENCE  =&gt; [ @old_values ],
    LOOKUP    =&gt; { %some_table },
    THATCODE  =&gt; \&amp;some_function,
    THISCODE  =&gt; sub { $_[0] ** $_[1] },
    HANDLE    =&gt; \*STDOUT,
};

print $rec-&gt;{TEXT};

print $rec-&gt;{SEQUENCE}[0];
$last = pop @{ $rec-&gt;{SEQUENCE} };

print $rec-&gt;{LOOKUP}{&quot;key&quot;};
($first_k, $first_v) = each %{ $rec-&gt;{LOOKUP} };

# no difference calling named or anonymous subs
$answer = &amp;{ $rec-&gt;{THATCODE} }($arg);
$answer = &amp;{ $rec-&gt;{THISCODE} }($arg1, $arg2);

# must have extra braces on indirect object slot
print { $rec-&gt;{HANDLE} } &quot;a string\n&quot;;

use FileHandle;
$rec-&gt;{HANDLE}-&gt;autoflush(1);
$rec-&gt;{HANDLE}-&gt;print(&quot;a string\n&quot;);</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.5.2">4.7.5.2 Composition of more elaborate records</A
></H4
><pre class="programlisting">%TV = (
    flintstones =&gt; {
        series   =&gt; &quot;flintstones&quot;,
        nights   =&gt; [ qw(monday thursday friday) ],
        members  =&gt; [
            { name =&gt; &quot;fred&quot;,    role =&gt; &quot;lead&quot;, age  =&gt; 36, },
            { name =&gt; &quot;wilma&quot;,   role =&gt; &quot;wife&quot;, age  =&gt; 31, },
            { name =&gt; &quot;pebbles&quot;, role =&gt; &quot;kid&quot;,  age  =&gt;  4, },
        ],
    },

    jetsons     =&gt; {
        series   =&gt; &quot;jetsons&quot;,
        nights   =&gt; [ qw(wednesday saturday) ],
        members  =&gt; [
            { name =&gt; &quot;george&quot;,  role =&gt; &quot;lead&quot;, age  =&gt; 41, },
            { name =&gt; &quot;jane&quot;,    role =&gt; &quot;wife&quot;, age  =&gt; 39, },
            { name =&gt; &quot;elroy&quot;,   role =&gt; &quot;kid&quot;,  age  =&gt;  9, },
        ],
     },

    simpsons    =&gt; {
        series   =&gt; &quot;simpsons&quot;,
        nights   =&gt; [ qw(monday) ],
        members  =&gt; [
            { name =&gt; &quot;homer&quot;, role =&gt; &quot;lead&quot;, age =&gt; 34, },
            { name =&gt; &quot;marge&quot;, role =&gt; &quot;wife&quot;, age =&gt; 37, },
            { name =&gt; &quot;bart&quot;,  role =&gt; &quot;kid&quot;,  age =&gt; 11, },
        ],
     },
  );</PRE
></DIV
><div class="sect3"><h4 class="sect3"><a class="title" name="PERL2-CH-4-SECT-7.5.3">4.7.5.3 Generation of a hash of complex records</A
></H4
><p class="para"><a class="indexterm" name="AUTOID-15097"></A
>Because Perl is quite good at parsing complex data structures, you
might just put your data declarations in a separate file as regular
Perl code and then load them in with <a class="xref" href="ch03_028.htm#PERL2-CMD-DO" title="do"><b class="xref.cmd">do</B
></A
>
or <a class="xref" href="ch03_123.htm#PERL2-CMD-REQUIRE" title="require"><b class="xref.cmd">require</B
></A
>.  See <a class="xref" href="ch03_01.htm" title="Functions">Chapter 3, <cite class="chapter">Functions</CITE
></A
>, for details on those functions.</P
><pre class="programlisting"># here's a piece by piece build up
$rec = {};
$rec-&gt;{series} = &quot;flintstones&quot;;
$rec-&gt;{nights} = [ find_days() ];

@members = ();
# assume this file is in field=value syntax
while (&lt;&gt;) {
     %fields = split /[\s=]+/;
     push @members, { %fields };
}
$rec-&gt;{members} = [ @members ];

# now remember the whole thing
$TV{ $rec-&gt;{series} } = $rec;</PRE
><p class="para">You can use extra pointer fields to avoid duplicate data.  For example,
you might want a <code class="literal">&quot;kids&quot;</CODE
> field included in a person's record.
This could be a reference
to a list consisting of references to the kids' own records.
That way you avoid the update problems that result from having the
same data in two places.</P
><pre class="programlisting">foreach $family (keys %TV) {
    my $rec = $TV{$family}; # temp pointer
    @kids = ();
    for $person ( @{$rec-&gt;{members}} ) {
        if ($person-&gt;{role} =~ /kid|son|daughter/) {
            push @kids, $person;
        }
    }
    # REMEMBER: $rec and $TV{$family} point to same data!!
    $rec-&gt;{kids} = [ @kids ];
}
# you copied the array, but the array itself contains pointers to
# uncopied objects. this means that if you make bart get older via

$TV{simpsons}{kids}[0]{age}++;

# then this would also change here
print $TV{simpsons}{members}[2]{age};

# because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]
# both point to the same underlying anonymous hash table

# print the whole thing
foreach $family ( keys %TV ) {
    print &quot;the $family&quot;;
    print &quot; is on during @{ $TV{$family}{nights} }\n&quot;;
    print &quot;its members are:\n&quot;;
    for $who ( @{ $TV{$family}{members} } ) {
        print &quot; $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n&quot;;
    }
    print &quot;it turns out that $TV{$family}{'lead'} has &quot;;
    print scalar ( @{ $TV{$family}{kids} } ), &quot; kids named &quot;;
    print join (&quot;, &quot;, map { $_-&gt;{name} } @{ $TV{$family}{kids} } );
    print &quot;\n&quot;;
}</PRE
><a class="indexterm" name="AUTOID-15107"></A
><a class="indexterm" name="AUTOID-15108"></A
><a class="indexterm" name="AUTOID-15109"></A
><a class="indexterm" name="AUTOID-15110"></A
><a class="indexterm" name="AUTOID-15113"></A
><a class="indexterm" name="AUTOID-15116"></A
><a class="indexterm" name="AUTOID-15119"></A
><a class="indexterm" name="AUTOID-15122"></A
><a class="indexterm" name="AUTOID-15125"></A
><a class="indexterm" name="AUTOID-15128"></A
></DIV
></DIV
></DIV
><div class="htmlnav"><p></P
><hr align="LEFT" width="515" title="footer"><table width="515" border="0" cellspacing="0" cellpadding="0"><tr><td align="LEFT" valign="TOP" width="172"><a class="SECT1" href="ch04_06.htm" title="4.6 A Brief Tutorial:  Manipulating Lists of Lists"><img src="../gifs/txtpreva.gif" alt="Previous: 4.6 A Brief Tutorial:  Manipulating Lists of Lists" border="0"></A
></TD
><td align="CENTER" valign="TOP" width="171"><a class="book" href="index.htm" title="Programming Perl"><img src="../gifs/txthome.gif" alt="Programming Perl" border="0"></A
></TD
><td align="RIGHT" valign="TOP" width="172"><a class="CHAPTER" href="ch05_01.htm" title="5. Packages, Modules, and Object Classes"><img src="../gifs/txtnexta.gif" alt="Next: 5. Packages, Modules, and Object Classes" border="0"></A
></TD
></TR
><tr><td align="LEFT" valign="TOP" width="172">4.6 A Brief Tutorial:  Manipulating Lists of Lists</TD
><td align="CENTER" valign="TOP" width="171"><a class="index" href="index/idx_0.htm" title="Book Index"><img src="../gifs/index.gif" alt="Book Index" border="0"></A
></TD
><td align="RIGHT" valign="TOP" width="172">5. Packages, Modules, and Object Classes</TD
></TR
></TABLE
><hr align="LEFT" width="515" title="footer"><p class="nav"><font size="-1">[ <a href="../index.html" title="The Perl CD Bookshelf">Library Home</A
> | <a href="../perlnut/index.htm" title="Perl in a Nutshell">Perl in a Nutshell</A
> | <a href="../learn/index.htm" title="Learning Perl">Learning Perl</A
> | <a href="../learn32/index.htm" title="Learning Perl on Win32 Systems">Learning Perl on Win32</A
> | <a href="index.htm" title="Programming Perl">Programming Perl</A
> | <a href="../advprog/index.htm" title="Advanced Perl Programming">Advanced Perl Programming</A
> | <a href="../cookbook/index.htm" title="Perl Cookbook">Perl Cookbook</A
> ]</FONT
></P
></DIV
></BODY
></HTML
>
