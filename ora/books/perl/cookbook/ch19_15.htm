<html><head>
<title>Recipe 19.14. Program: chemiserie</TITLE>
<meta name="DC.title" content="Perl Cookbook"><meta name="DC.creator" content="Tom Christiansen &amp; Nathan Torkington"><meta name="DC.publisher" content="O'Reilly &amp; Associates, Inc."><meta name="DC.date" content="1999-07-02T01:45:48Z"><meta name="DC.type" content="Text.Monograph"><meta name="DC.format" content="text/html" scheme="MIME"><meta name="DC.source" content="1-56592-243-3" scheme="ISBN"><meta name="DC.language" content="en-US"><meta name="generator" content="Jade 1.1/O'Reilly DocBook 3.0 to HTML 4.0"><link rev="made" href="mailto:online-books@oreilly.com" title="Online Books Comments"><link rel="up" href="ch19_01.htm" title="19. CGI Programming"><link rel="prev" href="ch19_14.htm" title="19.13. Saving a Form to a File or Mail Pipe"><link rel="next" href="ch20_01.htm" title="20. Web Automation"></HEAD
><body bgcolor="#FFFFFF" text="#000000"><div class="htmlnav"><h1><img src="gifs/smbanner.gif" alt="Perl Cookbook" usemap="#srchmap" border="0"></H1
><map name=index.html"srchmap"><area shape="RECT" coords="0,0,466,65" href="index.htm" alt="Perl Cookbook"><area shape="RECT" coords="467,0,514,18" href="../search/csrch.htm" alt="Search this book"></MAP
><table width="515" border="0" cellspacing="0" cellpadding="0"><tr><td align="LEFT" valign="TOP" width="172"><a class="sect1" href="ch19_14.htm" title="19.13. Saving a Form to a File or Mail Pipe"><img src="../gifs/txtpreva.gif" alt="Previous: 19.13. Saving a Form to a File or Mail Pipe" border="0"></A
></TD
><td align="CENTER" valign="TOP" width="171"><b><font face="ARIEL,HELVETICA,HELV,SANSERIF" size="-1"><a class="chapter" rel="up" href="ch19_01.htm" title="19. CGI Programming">Chapter 19<br>CGI Programming</A
></FONT
></B
></TD
><td align="RIGHT" valign="TOP" width="172"><a class="chapter" href="ch20_01.htm" title="20. Web Automation"><img src="../gifs/txtnexta.gif" alt="Next: 20. Web Automation" border="0"></A
></TD
></TR
></TABLE
>&nbsp;<hr align="LEFT" width="515" title="footer"></DIV
><div class="sect1"><h2 class="sect1"><a class="title" name="ch19-17893">19.14. Program: chemiserie</A
></H2
><p class="para"><a class="indexterm" name="ch19-idx-1000005533-0"></A
><a class="indexterm" name="ch19-idx-1000005533-1"></A
>The CGI script in <a class="xref" href="#ch19-42370" title="chemiserie">Example 19.9</A
> lets people order t-shirts and sweaters over the Web, using techniques described in <a class="xref" href="ch19_13.htm" title="Writing a Multiscreen CGI Script">Recipe 19.12</A
>. Its output isn't elegant or beautiful, but illustrating the multiscreen technique in a short program was challenging enough without trying to make it pretty as well.</P
><p class="para">The <code class="literal">shirt</CODE
> and <code class="literal">sweater</CODE
> subroutines check their widget values. If the user somehow submits an invalid color or size, the value is reset to the first in the list of allowable colors or sizes.</P
><div class="example"><h4 class="example"><a class="title" name="ch19-42370">Example 19.9: chemiserie</A
></H4
><pre class="programlisting">#!/usr/bin/perl -w
# chemiserie - simple CGI shopping for shirts and sweaters

use strict;
use CGI qw(:standard);
use CGI::Carp qw(fatalsToBrowser);

my %States;             # state table mapping pages to functions
my $Current_Screen;     # the current screen

# Hash of pages and functions.

%States = (
    'Default'     =&gt; \&amp;front_page,
    'Shirt'       =&gt; \&amp;shirt,
    'Sweater'     =&gt; \&amp;sweater,
    'Checkout'    =&gt; \&amp;checkout,
    'Card'        =&gt; \&amp;credit_card,
    'Order'       =&gt; \&amp;order,
    'Cancel'      =&gt; \&amp;front_page,
);

$Current_Screen = param(&quot;.State&quot;) || &quot;Default&quot;;
die &quot;No screen for $Current_Screen&quot; unless $States{$Current_Screen};

# Generate the current page.

standard_header();

while (my($screen_name, $function) = each %States) {
    $function-&gt;($screen_name eq $Current_Screen);
}
standard_footer();
exit;


################################
# header, footer, menu functions
################################

sub standard_header {
    print header(), start_html(-Title =&gt; &quot;Shirts&quot;, -BGCOLOR=&gt;&quot;White&quot;);
    print start_form(); # start_multipart_form() if file upload
}

sub standard_footer { print end_form(), end_html() }

sub shop_menu {
    print p(defaults(&quot;Empty My Shopping Cart&quot;),
        to_page(&quot;Shirt&quot;),
        to_page(&quot;Sweater&quot;),
        to_page(&quot;Checkout&quot;));
}

#############################
# subroutines for each screen
#############################

# The default page.
sub front_page {
    my $active = shift;
    return unless $active;

    print &quot;&lt;H1&gt;Hi!&lt;/H1&gt;\n&quot;;
    print &quot;Welcome to our Shirt Shop!  Please make your selection from &quot;;
    print &quot;the menu below.\n&quot;;

    shop_menu();
}

# Page to order a shirt from.
sub shirt {
    my $active = shift;
    my @sizes  = qw(XL L M S);
    my @colors = qw(Black White);

    my ($size, $color, $count) =
      (param(&quot;shirt_size&quot;), param(&quot;shirt_color&quot;), param(&quot;shirt_count&quot;));

    # sanity check
    if ($count) {
        $color = $colors[0] unless grep { $_ eq $color } @colors;
        $size  = $sizes[0]  unless grep { $_ eq $size  } @sizes;
        param(&quot;shirt_color&quot;, $color);
        param(&quot;shirt_size&quot;,  $size);
    }

    unless ($active) {
        print hidden(&quot;shirt_size&quot;)  if $size;
        print hidden(&quot;shirt_color&quot;) if $color;
        print hidden(&quot;shirt_count&quot;) if $count;
        return;
    }

    print h1(&quot;T-Shirt&quot;);
    print p(&quot;What a shirt!  This baby is decked out with all the options.&quot;,
        &quot;It comes with full luxury interior, cotton trim, and a collar&quot;,
        &quot;to make your eyes water!  Unit price: \$33.00&quot;);

    print h2(&quot;Options&quot;);
    print p(&quot;How Many?&quot;, textfield(&quot;shirt_count&quot;));
    print p(&quot;Size?&quot;,  popup_menu(&quot;shirt_size&quot;,  \@sizes ),
        &quot;Color?&quot;, popup_menu(&quot;shirt_color&quot;, \@colors));

    shop_menu();
}

# Page to order a sweater from.
sub sweater {
    my $active = shift;
    my @sizes  = qw(XL L M);
    my @colors = qw(Chartreuse Puce Lavender);

    my ($size, $color, $count) =
      (param(&quot;sweater_size&quot;), param(&quot;sweater_color&quot;), param(&quot;sweater_count"));

    # sanity check
    if ($count) {
        $color = $colors[0] unless grep { $_ eq $color } @colors;
        $size  = $sizes[0]  unless grep { $_ eq $size  } @sizes;
        param(&quot;sweater_color&quot;, $color);
        param(&quot;sweater_size&quot;,  $size);
    }

    unless ($active) {
        print hidden(&quot;sweater_size&quot;)  if $size;
        print hidden(&quot;sweater_color&quot;) if $color;
        print hidden(&quot;sweater_count&quot;) if $count;
        return;
    }

    print h1(&quot;Sweater&quot;);
    print p(&quot;Nothing implies preppy elegance more than this fine&quot;,
        &quot;sweater.  Made by peasant workers from black market silk,&quot;,
        &quot;it slides onto your lean form and cries out ``Take me,&quot;,
        &quot;for I am a god!''.  Unit price: \$49.99.&quot;);

    print h2(&quot;Options&quot;);
    print p(&quot;How Many?&quot;, textfield(&quot;sweater_count&quot;));
    print p(&quot;Size?&quot;,  popup_menu(&quot;sweater_size&quot;,  \@sizes));
    print p(&quot;Color?&quot;, popup_menu(&quot;sweater_color&quot;, \@colors));

    shop_menu();
}

# Page to display current order for confirmation.
sub checkout {
    my $active = shift;

    return unless $active;

    print h1(&quot;Order Confirmation&quot;);
    print p(&quot;You ordered the following:&quot;);
    print order_text();
    print p(&quot;Is this right?  Select 'Card' to pay for the items&quot;,
        &quot;or 'Shirt' or 'Sweater' to continue shopping.&quot;);
    print p(to_page(&quot;Card&quot;),
        to_page(&quot;Shirt&quot;), 
        to_page(&quot;Sweater&quot;));
}

# Page to gather credit-card information.
sub credit_card {
    my $active = shift;
    my @widgets = qw(Name Address1 Address2 City Zip State Phone Card Expiry);

    unless ($active) {
        print map { hidden($_) } @widgets;
        return;
    }

    print pre(p(&quot;Name:          &quot;, textfield(&quot;Name&quot;)),
      p(&quot;Address:       &quot;, textfield(&quot;Address1&quot;)),
      p(&quot;               &quot;, textfield(&quot;Address2&quot;)),
      p(&quot;City:          &quot;, textfield(&quot;City&quot;)),
      p(&quot;Zip:           &quot;, textfield(&quot;Zip&quot;)),
      p(&quot;State:         &quot;, textfield(&quot;State&quot;)),
      p(&quot;Phone:         &quot;, textfield(&quot;Phone&quot;)),
      p(&quot;Credit Card #: &quot;, textfield(&quot;Card&quot;)),
      p(&quot;Expiry:        &quot;, textfield(&quot;Expiry&quot;)));

    print p(&quot;Click on 'Order' to order the items.  Click on 'Cancel' to return shopping.");

    print p(to_page(&quot;Order&quot;), to_page(&quot;Cancel&quot;));
}

# Page to complete an order.
sub order {
    my $active = shift;

    unless ($active) {
        return;
    }

    # you'd check credit card values here

    print h1(&quot;Ordered!&quot;);
    print p(&quot;You have ordered the following toppings:&quot;);
    print order_text();

    print p(defaults(&quot;Begin Again&quot;));
}

# Returns HTML for the current order (&quot;You have ordered ...&quot;)
sub order_text {
    my $html = '';

    if (param(&quot;shirt_count&quot;)) {
        $html .= p(&quot;You have ordered &quot;, param(&quot;shirt_count&quot;),
           &quot; shirts of size &quot;,  param(&quot;shirt_size&quot;),
           &quot; and color &quot;, param(&quot;shirt_color&quot;), &quot;.&quot;);
    }
    if (param(&quot;sweater_count&quot;)) {
        $html .= p(&quot;You have ordered &quot;,  param(&quot;sweater_count&quot;),
           &quot; sweaters of size &quot;, param(&quot;sweater_size&quot;),
           &quot; and color &quot;, param(&quot;sweater_color&quot;), &quot;.&quot;);
    }
    $html = p(&quot;Nothing!&quot;) unless $html;
    $html .= p(&quot;For a total cost of &quot;, calculate_price());
    return $html;
}


sub calculate_price {
    my $shirts   = param(&quot;shirt_count&quot;)   || 0;
    my $sweaters = param(&quot;sweater_count&quot;) || 0;
    return sprintf(&quot;\$%.2f&quot;, $shirts*33 + $sweaters * 49.99);
}
<a class="indexterm" name="ch19-idx-1000005538-0"></A
><a class="indexterm" name="ch19-idx-1000005538-1"></A
>
sub to_page { submit(-NAME =&gt; &quot;.State&quot;, -VALUE =&gt; shift) }<a class="indexterm" name="ch19-idx-1000005330-0"></A
></PRE
></DIV
></DIV
><div class="htmlnav"><p></P
><hr align="LEFT" width="515" title="footer"><table width="515" border="0" cellspacing="0" cellpadding="0"><tr><td align="LEFT" valign="TOP" width="172"><a class="sect1" href="ch19_14.htm" title="19.13. Saving a Form to a File or Mail Pipe"><img src="../gifs/txtpreva.gif" alt="Previous: 19.13. Saving a Form to a File or Mail Pipe" border="0"></A
></TD
><td align="CENTER" valign="TOP" width="171"><a class="book" href="index.htm" title="Perl Cookbook"><img src="../gifs/txthome.gif" alt="Perl Cookbook" border="0"></A
></TD
><td align="RIGHT" valign="TOP" width="172"><a class="chapter" href="ch20_01.htm" title="20. Web Automation"><img src="../gifs/txtnexta.gif" alt="Next: 20. Web Automation" border="0"></A
></TD
></TR
><tr><td align="LEFT" valign="TOP" width="172">19.13. Saving a Form to a File or Mail Pipe</TD
><td align="CENTER" valign="TOP" width="171"><a class="index" href="index/idx_0.htm" title="Book Index"><img src="../gifs/index.gif" alt="Book Index" border="0"></A
></TD
><td align="RIGHT" valign="TOP" width="172">20. Web Automation</TD
></TR
></TABLE
><hr align="LEFT" width="515" title="footer"><p class="nav"><font size="-1">[ <a href="../index.html" title="The Perl CD Bookshelf">Library Home</A
> | <a href="../perlnut/index.htm" title="Perl in a Nutshell">Perl in a Nutshell</A
> | <a href="../learn/index.htm" title="Learning Perl">Learning Perl</A
> | <a href="../learn32/index.htm" title="Learning Perl on Win32 Systems">Learning Perl on Win32</A
> | <a href="../prog/index.htm" title="Programming Perl">Programming Perl</A
> | <a href="../advprog/index.htm" title="Advanced Perl Programming">Advanced Perl Programming</A
> | <a href="index.htm" title="Perl Cookbook">Perl Cookbook</A
> ]</FONT
></P
></DIV
></BODY
></HTML
>
