<html><head><title>Tk::Trace (Mastering Perl/Tk)</title><link rel="stylesheet" type="text/css" href="../style/style1.css" />

<meta name="DC.Creator" content="Steve Lidie and Nancy Walsh" /><meta name="DC.Format" content="text/xml" scheme="MIME" /><meta name="DC.Language" content="en-US" /><meta name="DC.Publisher" content="O'Reilly &amp; Associates, Inc." /><meta name="DC.Source" scheme="ISBN" content="1565927168L" /><meta name="DC.Subject.Keyword" content="stuff" /><meta name="DC.Title" content="Mastering Perl/Tk" /><meta name="DC.Type" content="Text.Monograph" />

</head><body bgcolor="#ffffff">

<img src="gifs/smbanner.gif" usemap="#banner-map" border="0" alt="Book Home" /><map name="banner-map"><area shape="rect" coords="1,-2,616,66" href="index.htm" alt="Mastering Perl/Tk" /><area shape="rect" coords="629,-11,726,25" href="jobjects/fsearch.htm" alt="Search this book" /></map>

<div class="navbar"><table width="684" border="0"><tr><td align="left" valign="top" width="228"><a href="appc_05.htm"><img src="../gifs/txtpreva.gif" alt="Previous" border="0" /></a></td><td align="center" valign="top" width="228" /><td align="right" valign="top" width="228"><a href="appc_07.htm"><img src="../gifs/txtnexta.gif" alt="Next" border="0" /></a></td></tr></table></div>



<h2 class="sect1">C.6. Tk::Trace</h2>

<p>The Tk::Trace module can be used to trace Perl/Tk variables. See
<a href="ch15_01.htm">Chapter 15, "Anatomy of the MainLoop"</a> for explanation and demonstration.
</p>

<blockquote><pre class="code">$Tk::Trace::VERSION = '1.0';

package Tk::Trace;

use Exporter;
use base qw/Exporter/;
@EXPORT = qw/traceVariable traceVdelete traceVinfo/;
use Tie::Watch;
use strict;

my %trace;                      # watchpoints indexed by stringified ref
my %op = (                      # map Tcl op to tie function
    'r' =&gt; ['-fetch',   \&amp;fetch],
    'w' =&gt; ['-store',   \&amp;store],
    'u' =&gt; ['-destroy', \&amp;destroy],
);

sub fetch {

    # fetch() wraps the user's callback with necessary tie( ) bookkeeping
    # and invokes the callback with the proper arguments. It expects:
    #
    # $_[0] = Tie::Watch object
    # $_[1] = undef for a scalar, an index/key for an array/hash
    #
    # The user's callback is passed these arguments:
    #
    #   $_[0]        = undef for a scalar, index/key for array/hash
    #   $_[1]        = current value
    #   $_[2]        = operation (r, w, or u)
    #   $_[3 .. $#_] = optional user callback arguments
    #
    # The user callback returns the final value to assign the variable.

    my $self = shift;		                          # Tie::Watch object
    my $val = $self-&gt;Fetch(@_);	                # get variable's current value
    my $aref = $self-&gt;Args(-fetch);          # argument reference
    my $sub = shift @$aref;	                    # user's callback
    unshift @_, undef if scalar @_ == 0;     # undef "index" for a scalar
    my @args = @_;                           # save for post-callback work
    $args[1] = &amp;$sub(@_, $val, 'r', @$aref); # invoke user callback
    shift @args unless defined $args[0];     # drop scalar "index"
    $self-&gt;Store(@args);                     # update variable's value

} # end fetch

sub store {

    # store() wraps the user's callback with necessary tie( ) bookkeeping
    # and invokes the callback with the proper arguments. It expects:
    #
    # $_[0] = Tie::Watch object
    # $_[1] = new value for a scalar, index/key for an array/hash
    # $_[2] = undef for a scalar, new value for an array/hash
    #
    # The user's callback is passed these arguments:
    #
    #   $_[0]        = undef for a scalar, index/key for array/hash
    #   $_[1]        = new value
    #   $_[2]        = operation (r, w, or u)
    #   $_[3 .. $#_] = optional user callback arguments
    #
    # The user callback returns the final value to assign the variable.

    my $self = shift;		                         # Tie::Watch object
    $self-&gt;Store(@_);                        # store variable's new value
    my $aref = $self-&gt;Args(-store);          # argument reference
    my $sub = shift @$aref;                  # user's callback
    unshift @_, undef if scalar @_ == 1;     # undef "index" for a scalar
    my @args = @_;                           # save for post-callback work
    $args[1] = &amp;$sub(@_, 'w', @$aref);       # invoke user callback 
    shift @args unless defined $args[0];     # drop scalar "index"
    $self-&gt;Store(@args);                     # update variable's value

} # end store

sub destroy {
    my $self = shift;
    my $aref = $self-&gt;Args(-destroy);        # argument reference
    my $sub = shift @$aref;	                    # user's callback
    my $val = $self-&gt;Fetch(@_);              # get final value
    &amp;$sub(undef, $val, 'u', @$aref);         # invoke user callback
    $self-&gt;Destroy(@_);                      # destroy variable
}

sub traceVariable {
    my($parent, $vref, $op, $callback) = @_;
    die "Illegal parent." unless ref $parent;
    die "Illegal variable." unless ref $vref;
    die "Illegal trace operation '$op'." unless $op;
    die "Illegal trace operation '$op'." if $op =~ /[^rwu]/;
    die "Illegal callback." unless $callback;

    # Need to add our internal callback to user's callback arg list
    # so we can call it first, followed by the user's callback and
    # any user arguments.

    my($fetch, $store, $destroy);
    if (ref $callback eq 'CODE') {
        $fetch   = [\&amp;fetch,   $callback];
        $store   = [\&amp;store,   $callback];
        $destroy = [\&amp;destroy, $callback];
    } else {                    # assume [] form
        $fetch   = [\&amp;fetch,   @$callback]; 
        $store   = [\&amp;store,   @$callback]; 
        $destroy = [\&amp;destroy, @$callback];
    }
    
    my @wargs;
    push @wargs, (-fetch   =&gt; $fetch)   if $op =~ /r/;
    push @wargs, (-store   =&gt; $store)   if $op =~ /w/;
    push @wargs, (-destroy =&gt; $destroy) if $op =~ /u/;
    my $watch = Tie::Watch-&gt;new(
        -variable =&gt; $vref,
        @wargs,
    );

    $trace{$vref} = $watch;
    
} # end traceVariable

sub traceVdelete {
    my($parent, $vref, $op_not_honored, $callabck_not_honored) = @_;
    if (defined $trace{$vref}) {
        $trace{$vref}-&gt;Unwatch;
        delete $trace{$vref};
    }
}

sub traceVinfo {
    my($parent, $vref) = @_;
    return (defined $trace{$vref}) ? $trace{$vref}-&gt;Info : undef;
}

1;
__END__

=head1 NAME

Tk::Trace - emulate Tcl/Tk B&lt;trace&gt; functions.

=head1 SYNOPSIS

 use Tk::Trace;

 $mw-&gt;traceVariable(\$v, 'wru' =&gt; [\&amp;update_meter, $scale]);
 %vinfo = $mw-&gt;traceVinfo(\$v);
 print "Trace info  :\n  ", join("\n  ", @{$vinfo{-legible}}), "\n";
 $mw-&gt;traceVdelete(\$v);

=head1 DESCRIPTION

This class module emulates the Tcl/Tk B&lt;trace&gt; family of commands by
binding subroutines of your devising to Perl variables using simple
B&lt;Tie::Watch&gt; features.

Callback format is patterned after the Perl/Tk scheme: supply either a
code reference, or, supply an array reference and pass the callback
code reference in the first element of the array, followed by callback
arguments.

User callbacks are passed these arguments:

 $_[0]        = undef for a scalar, index/key for array/hash
 $_[1]        = variable's current (read), new (write), final (undef) value
 $_[2]        = operation (r, w, or u)
 $_[3 .. $#_] = optional user callback arguments

As a Trace user, you have an important responsibility when writing your 
callback, since you control the final value assigned to the variable.
A typical callback might look like:

 sub callback {
    my($index, $value, $op, @args) = @_;
    return if $op eq 'u';
    # .... code which uses $value ...
    return $value;     # variable's final value
 }

Note that the callback's return value becomes the variable's final value,
for either read or write traces.

For write operations, the variable is updated with its new value before
the callback is invoked. 

Only one callback can be attached to a variable, but read, write and undef
operations can be traced simultaneously.

=head1 METHODS

=over 4

=item $mw-&gt;traceVariable(varRef, op =&gt; callback);

B&lt;varRef&gt; is a reference to the scalar, array or hash variable you
wish to trace.  B&lt;op&gt; is the trace operation, and can be any combination
of B&lt;r&gt; for read, B&lt;w&gt; for write, and B&lt;u&gt; for undef.  B&lt;callback&gt; is a
standard Perl/Tk callback, and is invoked, depending upon the value of
B&lt;op&gt;, whenever the variable is read, written, or destroyed.

=item %vinfo = $mw-&gt;traceVinfo(varRef);

Returns a hash detailing the internals of the Trace object, with these
keys:

 %vinfo = (
     -variable =&gt;  varRef
     -debug    =&gt;  '0'
     -shadow   =&gt;  '1'
     -value    =&gt;  'HELLO SCALAR'
     -destroy  =&gt;  callback
     -fetch    =&gt;  callback
     -store    =&gt;  callback
     -legible  =&gt;  above data formatted as a list of string, for printing
 );

For array and hash Trace objects, the B&lt;-value&gt; key is replaced with a
B&lt;-ptr&gt; key which is a reference to the parallel array or hash.
Additionally, for an array or hash, there are key/value pairs for
all the variable specific callbacks.

=item $mw-&gt;traceVdelete(\$v);

Stop tracing the variable.

=back

=head1 EXAMPLE

 #!/usr/local/bin/perl -w
 #
 # Trace a Scale's variable and move a meter in unison.

 package Tk;
     use Tk::Trace;

 package main;

 use Tk;
 use constant PI =&gt; 3.1415926;
 use strict;

 my $mw = MainWindow-&gt;new;

 my $c = $mw-&gt;Canvas(qw/-width 200 -height 110 -bd 2 -relief sunken/)-&gt;grid;
 $c-&gt;createLine(qw/ 100 100 10 100  -tag meter -arrow last -width 5/);
 my $s = $mw-&gt;Scale(qw/-orient h -from 0 -to 100 -variable/ =&gt; \my $v)-&gt;grid;
 $mw-&gt;Label(-text =&gt; 'Slide Me for &gt; 5 Seconds')-&gt;grid;

 $mw-&gt;traceVariable(\$v, 'w' =&gt; [\&amp;update_meter, $c, $s]);

 $mw-&gt;after(5000 =&gt; sub {
     print "Untrace time ...\n";
     my %vinfo = $s-&gt;traceVinfo(\$v);
     print "Watch info  :\n  ", join("\n  ", @{$vinfo{-legible}}), "\n";
     $c-&gt;traceVdelete(\$v);
 });

 MainLoop;

 sub update_meter {
     my($index, $value, $op, @args) = @_;
     return if $op eq 'u';
     my($c, $s) = @args[0,1];	# Canvas and Scale widgets
     my($min, $max) = ($s-&gt;cget(-from), $s-&gt;cget(-to));
     my $pos = $value / abs($max - $min);
     my $x = 100.0 - 90.0 * (cos( $pos * PI ));
     my $y = 100.0 - 90.0 * (sin( $pos * PI ));
     $c-&gt;coords(qw/meter 100 100/, $x, $y);
     return $value;
 }

=head1 HISTORY

 Stephen.O.Lidie@Lehigh.EDU, Lehigh University Computing Center, 2000/08/01
 . Version 1.0, for Tk800.022.

=head1 COPYRIGHT

Copyright (C) 2000 - 2001 Stephen O. Lidie. All rights reserved.

This program is free software; you can redistribute it and/or modify it under
the same terms as Perl itself.

=cut</pre></blockquote>



<hr width="684" align="left" />
<div class="navbar"><table width="684" border="0"><tr><td align="left" valign="top" width="228"><a href="appc_05.htm"><img src="../gifs/txtpreva.gif" alt="Previous" border="0" /></a></td><td align="center" valign="top" width="228"><a href="index.htm"><img src="../gifs/txthome.gif" alt="Home" border="0" /></a></td><td align="right" valign="top" width="228"><a href="appc_07.htm"><img src="../gifs/txtnexta.gif" alt="Next" border="0" /></a></td></tr><tr><td align="left" valign="top" width="228">C.5. tkmpg123</td><td align="center" valign="top" width="228"><a href="index/index.htm"><img src="../gifs/index.gif" alt="Book Index" border="0" /></a></td><td align="right" valign="top" width="228">C.7. tkhp16c</td></tr></table></div>
<hr width="684" align="left" />

<img src="../gifs/navbar.gif" usemap="#library-map" border="0" alt="Library Navigation Links" />
<p><p><font size="-1"><a href="copyrght.htm">Copyright &copy; 2002</a> O'Reilly &amp; Associates. All rights reserved.</font></p>

<map name="library-map"><area shape="rect" coords="1,0,85,94" href="../index.htm"><area shape="rect" coords="86,1,178,103" href="../lwp/index.htm"><area shape="rect" coords="180,0,265,103" href="../lperl/index.htm"><area shape="rect" coords="267,0,353,105" href="../perlnut/index.htm"><area shape="rect" coords="354,1,446,115" href="../prog/index.htm"><area shape="rect" coords="448,0,526,132" href="index.htm"><area shape="rect" coords="528,1,615,119" href="../cookbook/index.htm"><area shape="rect" coords="617,0,690,135" href="../pxml/index.htm"></map>

</body></html>